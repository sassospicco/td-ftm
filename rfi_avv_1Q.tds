# Segnale di avviso puro a vela quadra, seguito da un segnale imperativo.
# Aspetti: G, Gx, V

# Pure distant signal with 1 square head, followed by a main signal.
# Aspects: Y, Yx, G

# P.Rosati  15/6/2015

Aspect: yellow
	IconE: NQ_y_E.xpm
	IconW: NQ_y_W.xpm
	IconN: NQ_y_N.xpm
	IconS: NQ_y_S.xpm
	Action: none

Aspect: yellow_flashing
	IconE: NQ_y_E.xpm NQ_b_E.xpm
	IconW: NQ_y_W.xpm NQ_b_W.xpm
	IconN: NQ_y_N.xpm NQ_b_N.xpm
	IconS: NQ_y_S.xpm NQ_b_S.xpm
	Action: none

Aspect: green
	IconE: NQ_g_E.xpm
	IconW: NQ_g_W.xpm
	IconN: NQ_g_N.xpm
	IconS: NQ_g_S.xpm
	Action: none

# initial aspect

OnInit:
	.aspect = yellow
end

# user opened the signal

OnCleared:
	return
end

# another signal changed aspect

OnUpdate:		
	# Prossimo è deviato (anche forzato) o segnale di partenza
	# Next signal is diverging (or forced to) or is an exit signal
	if next.switchThrown = 1 or next.nextApproach.script = "rfi_avv_dev.tds" or next.aspect = yellow_yellow or next.script > "rfi_pars" and next.script < "rfi_paru"
		.aspect = yellow
		return
	end
	# Prossimo = R o equivalente (S | RGG), anche forzato da un avviso rosso.
	# Next is R or equivalent (S | RYY) or forced so by a distant at red.
	if nextApproach.aspect = red or nextApproach.aspect = red_short or next.aspect = red or next.aspect = shunt or next.aspect = red_yellow_yellow
		.aspect = yellow	# Segnale incompleto - Incomplete aspect
	else 
		# Prossimo = G o avviso di deviata (GV | GxVx | Gx/Vx)
		# Next is Y or prepare to slow (YG | YxGx | Yx/Gx)
		if next.aspect = yellow or next.aspect = yellow_green or next.aspect = yellow_green_flashing or next.aspect = yellow_green_switching
			if next.nextLength < 1200
				.aspect = yellow_flashing
				return
			end
		end			
		# Altri casi (Gx | V), compreso nessun altro segnale (uscita)
		# Other cases (next is Yx or G), including no more signals (exit point)
		.aspect = green
	end
end
